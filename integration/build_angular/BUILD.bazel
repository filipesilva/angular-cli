load("//tools/npm_integration_test:npm_integration_test.bzl", "npm_integration_test")

filegroup(
    name = "build_angular_sources",
    srcs = glob(
        include = ["**/*"],
        exclude = [
          "node_modules/**",
          ".yarn_local_cache/**",
        ],
    ),
)

INTEGRATION_TESTS = [
    "app-shell",
    "browser-1",
    "browser-2",
    "browser-3",
    "browser-4",
    "dev-server",
    "extract-i18n",
    "karma",
    "protractor",
    "server",
    "tslint",
]

[
    npm_integration_test(
        name = test_name,
        check_npm_packages = [
            "@angular-devkit/architect",
            "@angular-devkit/build-angular",
            "@angular-devkit/build-optimizer",
            "@angular-devkit/build-webpack",
            "@angular-devkit/core",
            "@ngtools/webpack",
        ],
        npm_packages = {
            "//packages/angular_devkit/architect:npm_package_archive": "@angular-devkit/architect",
            "//packages/angular_devkit/build_optimizer:npm_package_archive": "@angular-devkit/build-optimizer",
            "//packages/angular_devkit/build_webpack:npm_package_archive": "@angular-devkit/build-webpack",
            "//packages/angular_devkit/build_angular:npm_package_archive": "@angular-devkit/build-angular",
            "//packages/angular_devkit/core:npm_package_archive": "@angular-devkit/core",
            "//packages/ngtools/webpack:npm_package_archive": "@ngtools/webpack",
        },
        commands = [
            # Workaround https://github.com/yarnpkg/yarn/issues/2165
            # Yarn will cache file://dist URIs and not update
            "rm -rf ./.yarn_local_cache",
            "mkdir .yarn_local_cache",
            "patch-package-json",
            "$(rootpath @nodejs//:yarn_bin) install --cache-folder ./.yarn_local_cache",
            "$(rootpath @nodejs//:yarn_bin) test-" + test_name,
            "rm -rf ./.yarn_local_cache",
        ],
        data = [
            "@nodejs//:yarn_bin",
            "@nodejs//:yarn_files",
        ],
        test_files = ":build_angular_sources",
        size = "large",
        tags = ["exclusive"]
    )
    for test_name in INTEGRATION_TESTS
]
